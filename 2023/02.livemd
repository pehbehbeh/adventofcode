<!-- livebook:{"file_entries":[{"name":"02.txt","type":"attachment"}]} -->

# Advent of Code 2023 - Day 02

```elixir
Mix.install([
  {:kino, "~> 0.11.0"}
])
```

## Input

```elixir
input =
  Kino.FS.file_path("02.txt")
  |> File.read!()
  |> String.split("\n")
```

```elixir
games =
  input
  |> Enum.map(fn line ->
    [_ | rest] = String.split(line, [": ", "; "])

    Enum.map(rest, fn set ->
      String.split(set, ", ")
      |> Enum.into(%{}, fn cubes ->
        [count, color] = String.split(cubes, " ")
        {color, String.to_integer(count)}
      end)
    end)
  end)
```

```elixir
max_cubes =
  games
  |> Enum.map(fn sets ->
    Enum.reduce(sets, %{}, fn set, acc ->
      Map.merge(set, acc, fn _k, v1, v2 -> max(v1, v2) end)
    end)
  end)
```

## Part 1

```elixir
max = %{"red" => 12, "green" => 13, "blue" => 14}

max_cubes
|> Stream.with_index(1)
|> Stream.reject(fn {cubes, _i} ->
  Enum.any?(cubes, fn {color, count} -> count > max[color] end)
end)
|> Stream.map(fn {_cubes, i} -> i end)
|> Enum.sum()
```

## Part 2

```elixir
max_cubes
|> Stream.map(fn cubes -> Map.values(cubes) |> Enum.product() end)
|> Enum.sum()
```

[![Run in Livebook](https://livebook.dev/badge/v1/blue.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Fpehbehbeh%2Fadventofcode%2Fblob%2Fmain%2F2023%2F02.livemd)
