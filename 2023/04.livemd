# Advent of Code 2023 - Day 04

```elixir
Mix.install([
  {:kino, "~> 0.11.0"},
  {:kino_aoc, github: "ljgago/kino_aoc"}
])
```

## Input

<!-- livebook:{"attrs":{"assign_to":"puzzle_input","day":"4","session_secret":"AOC_SESSION","year":"2023"},"chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2023", "4", System.fetch_env!("LB_AOC_SESSION"))
```

```elixir
split = fn input -> String.split(input, " ", trim: true) end

match_counts =
  puzzle_input
  |> String.split("\n")
  |> Stream.map(fn line -> String.split(line, [": ", " | "]) end)
  |> Stream.map(fn [_card, left, right] -> {split.(left), split.(right)} end)
  |> Stream.map(fn {winners, numbers} -> winners -- winners -- numbers end)
  |> Stream.map(fn matches -> Enum.count(matches) end)
  |> Enum.with_index(1)
```

## Part 1

```elixir
match_counts
|> Stream.filter(fn {count, _} -> count > 0 end)
|> Stream.map(fn {count, _} -> Integer.pow(2, count - 1) end)
|> Enum.sum()
```

## Part 2

```elixir
for {count, i} <- match_counts,
    count > 0,
    j <- (i + 1)..(i + count),
    reduce: Map.new(match_counts, fn {_, i} -> {i, 1} end) do
  acc -> Map.put(acc, j, acc[j] + acc[i])
end
|> Map.values()
|> Enum.sum()
```

[![Run in Livebook](https://livebook.dev/badge/v1/blue.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Fpehbehbeh%2Fadventofcode%2Fblob%2Fmain%2F2023%2F04.livemd)

<!-- livebook:{"offset":1457,"stamp":{"token":"XCP.dYFingvHve434qPWVDzZ-nvaYOh1VoFRrAEU7WwQ2qdItNRm8uzH0VxkjR3Nj3zR5clskatYHcWdIBVrTTdI1PneEszNh9SBIWZCyatdy-opETQzMQA","version":2}} -->
